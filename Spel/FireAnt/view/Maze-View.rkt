(define (new-maze-view owner canvas)
  (let* ((floor-layer (canvas 'make-layer))
         (wall-layer (canvas 'make-layer))
         (height (owner 'get-height))
         (width (owner 'get-width))
         (tiles (new-matrix height width #f))
         (wall-bitmap (string-append bitmap-dir "wall.png"))
         (floor-bitmap (string-append bitmap-dir "floor.png")))

    (define (get-owner)
      owner)

    (define (draw)
      (draw-floor)
      (let outer-loop ((row 0))
        (if (< row height)
          (begin (let inner-loop ((column 0))
                   (if (< column width)
                     (begin (if (owner 'is-wall? row column)
                              (draw-wall row column))
                            (inner-loop (+ column 1)))))
                 (outer-loop (+ row 1))))))

    (define (draw-floor)
      (let ((tile (make-bitmap-tile floor-bitmap)))
        ((floor-layer 'add-drawable) tile)))

    (define (draw-wall row column)
      (let* ((size 40)
             (tile (make-bitmap-tile wall-bitmap))
             (pos_x (* column size))
             (pos_y (* row size)))
        (set-cell! tiles row column tile)
        ((wall-layer 'add-drawable) tile)
        ((tile 'set-x!) pos_x)
        ((tile 'set-y!) pos_y)))

    (define (dispatch cmd . args)
      (cond ((eq? cmd 'draw) (apply draw args))
            ((eq? cmd 'get-owner) (apply get-owner args))
            (else (error "Unknown command" cmd))))

    dispatch))
